const std = @import("std");
const c = @import("../../../c.zig").c;
const frame_orchestration = @import("rhi_frame_orchestration.zig");

pub fn waitIdle(ctx: anytype) void {
    if (!ctx.frames.dry_run and ctx.vulkan_device.vk_device != null) {
        _ = c.vkDeviceWaitIdle(ctx.vulkan_device.vk_device);
    }
}

pub fn setTextureUniforms(ctx: anytype, texture_enabled: bool) void {
    ctx.options.textures_enabled = texture_enabled;
    ctx.draw.descriptors_updated = false;
}

pub fn setViewport(ctx: anytype, width: u32, height: u32) void {
    const fb_w = width;
    const fb_h = height;
    _ = fb_w;
    _ = fb_h;

    var w: c_int = 0;
    var h: c_int = 0;
    _ = c.SDL_GetWindowSizeInPixels(ctx.window, &w, &h);

    if (!ctx.swapchain.skip_present and (@as(u32, @intCast(w)) != ctx.swapchain.getExtent().width or @as(u32, @intCast(h)) != ctx.swapchain.getExtent().height)) {
        ctx.runtime.framebuffer_resized = true;
    }

    if (!ctx.frames.frame_in_progress) return;

    const command_buffer = ctx.frames.command_buffers[ctx.frames.current_frame];

    var viewport = std.mem.zeroes(c.VkViewport);
    viewport.x = 0.0;
    viewport.y = 0.0;
    viewport.width = @floatFromInt(width);
    viewport.height = @floatFromInt(height);
    viewport.minDepth = 0.0;
    viewport.maxDepth = 1.0;
    c.vkCmdSetViewport(command_buffer, 0, 1, &viewport);

    var scissor = std.mem.zeroes(c.VkRect2D);
    scissor.offset = .{ .x = 0, .y = 0 };
    scissor.extent = .{ .width = width, .height = height };
    c.vkCmdSetScissor(command_buffer, 0, 1, &scissor);
}

pub fn getAllocator(ctx: anytype) std.mem.Allocator {
    return ctx.allocator;
}

pub fn getFrameIndex(ctx: anytype) usize {
    return @intCast(ctx.frames.current_frame);
}

pub fn supportsIndirectFirstInstance(ctx: anytype) bool {
    return ctx.vulkan_device.draw_indirect_first_instance;
}

pub fn recover(ctx: anytype) !void {
    if (!ctx.runtime.gpu_fault_detected) return;

    if (ctx.vulkan_device.recovery_count >= ctx.vulkan_device.max_recovery_attempts) {
        std.log.err("RHI: Max recovery attempts ({d}) exceeded. GPU is unstable.", .{ctx.vulkan_device.max_recovery_attempts});
        return error.GpuLost;
    }

    ctx.vulkan_device.recovery_count += 1;
    std.log.info("RHI: Attempting GPU recovery (Attempt {d}/{d})...", .{ ctx.vulkan_device.recovery_count, ctx.vulkan_device.max_recovery_attempts });

    _ = c.vkDeviceWaitIdle(ctx.vulkan_device.vk_device);

    ctx.runtime.gpu_fault_detected = false;
    ctx.mutex.lock();
    defer ctx.mutex.unlock();
    frame_orchestration.recreateSwapchainInternal(ctx);

    if (c.vkDeviceWaitIdle(ctx.vulkan_device.vk_device) != c.VK_SUCCESS) {
        std.log.err("RHI: Device unresponsive after recovery. Recovery failed.", .{});
        ctx.vulkan_device.recovery_fail_count += 1;
        ctx.runtime.gpu_fault_detected = true;
        return error.GpuLost;
    }

    ctx.vulkan_device.recovery_success_count += 1;
    std.log.info("RHI: Recovery step complete. If issues persist, please restart.", .{});
}

pub fn setWireframe(ctx: anytype, enabled: bool) void {
    if (ctx.options.wireframe_enabled != enabled) {
        ctx.options.wireframe_enabled = enabled;
        ctx.draw.terrain_pipeline_bound = false;
    }
}

pub fn setTexturesEnabled(ctx: anytype, enabled: bool) void {
    ctx.options.textures_enabled = enabled;
}

pub fn setDebugShadowView(ctx: anytype, enabled: bool) void {
    ctx.options.debug_shadows_active = enabled;
}

pub fn setVSync(ctx: anytype, enabled: bool) void {
    if (ctx.options.vsync_enabled == enabled) return;

    ctx.options.vsync_enabled = enabled;

    var mode_count: u32 = 0;
    _ = c.vkGetPhysicalDeviceSurfacePresentModesKHR(ctx.vulkan_device.physical_device, ctx.vulkan_device.surface, &mode_count, null);

    if (mode_count == 0) return;

    var modes: [8]c.VkPresentModeKHR = undefined;
    var actual_count: u32 = @min(mode_count, 8);
    _ = c.vkGetPhysicalDeviceSurfacePresentModesKHR(ctx.vulkan_device.physical_device, ctx.vulkan_device.surface, &actual_count, &modes);

    if (enabled) {
        ctx.options.present_mode = c.VK_PRESENT_MODE_FIFO_KHR;
    } else {
        ctx.options.present_mode = c.VK_PRESENT_MODE_FIFO_KHR;
        for (modes[0..actual_count]) |mode| {
            if (mode == c.VK_PRESENT_MODE_IMMEDIATE_KHR) {
                ctx.options.present_mode = c.VK_PRESENT_MODE_IMMEDIATE_KHR;
                break;
            } else if (mode == c.VK_PRESENT_MODE_MAILBOX_KHR) {
                ctx.options.present_mode = c.VK_PRESENT_MODE_MAILBOX_KHR;
            }
        }
    }

    ctx.runtime.framebuffer_resized = true;

    const mode_name: []const u8 = switch (ctx.options.present_mode) {
        c.VK_PRESENT_MODE_IMMEDIATE_KHR => "IMMEDIATE (VSync OFF)",
        c.VK_PRESENT_MODE_MAILBOX_KHR => "MAILBOX (Triple Buffer)",
        c.VK_PRESENT_MODE_FIFO_KHR => "FIFO (VSync ON)",
        c.VK_PRESENT_MODE_FIFO_RELAXED_KHR => "FIFO_RELAXED",
        else => "UNKNOWN",
    };
    std.log.info("Vulkan present mode: {s}", .{mode_name});
}

pub fn setAnisotropicFiltering(ctx: anytype, level: u8) void {
    if (ctx.options.anisotropic_filtering == level) return;
    ctx.options.anisotropic_filtering = level;
}

pub fn setVolumetricDensity(ctx: anytype, density: f32) void {
    _ = ctx;
    _ = density;
}

pub fn setMSAA(ctx: anytype, samples: u8) void {
    const clamped = @min(samples, ctx.vulkan_device.max_msaa_samples);
    if (ctx.options.msaa_samples == clamped) return;

    ctx.options.msaa_samples = clamped;
    ctx.swapchain.msaa_samples = clamped;
    ctx.runtime.framebuffer_resized = true;
    ctx.runtime.pipeline_rebuild_needed = true;
    std.log.info("Vulkan MSAA set to {}x (pending swapchain recreation)", .{clamped});
}

pub fn getMaxAnisotropy(ctx: anytype) u8 {
    return @intFromFloat(@min(ctx.vulkan_device.max_anisotropy, 16.0));
}

pub fn getMaxMSAASamples(ctx: anytype) u8 {
    return ctx.vulkan_device.max_msaa_samples;
}

pub fn getFaultCount(ctx: anytype) u32 {
    return ctx.vulkan_device.fault_count;
}

pub fn getValidationErrorCount(ctx: anytype) u32 {
    return ctx.vulkan_device.validation_error_count.load(.monotonic);
}
