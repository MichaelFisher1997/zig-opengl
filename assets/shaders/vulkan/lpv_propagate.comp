#version 450

layout(local_size_x = 4, local_size_y = 4, local_size_z = 4) in;

layout(set = 0, binding = 0, rgba32f) uniform readonly image2D lpv_src;
layout(set = 0, binding = 1, rgba32f) uniform writeonly image2D lpv_dst;

layout(push_constant) uniform PropPush {
    uint grid_size;
    vec4 propagation;
} push_data;

ivec2 atlasUV(ivec3 cell, int gridSize) {
    return ivec2(cell.x, cell.y + cell.z * gridSize);
}

vec3 sampleCell(ivec3 cell, int gridSize) {
    return imageLoad(lpv_src, atlasUV(cell, gridSize)).rgb;
}

void main() {
    int gridSize = int(push_data.grid_size);
    ivec3 cell = ivec3(gl_GlobalInvocationID.xyz);
    if (any(greaterThanEqual(cell, ivec3(gridSize)))) {
        return;
    }

    // propagation.x = neighbor propagation factor, propagation.y = center retention
    vec3 center = sampleCell(cell, gridSize) * push_data.propagation.y;
    vec3 accum = center;
    float f = push_data.propagation.x;

    ivec3 off[6] = ivec3[6](
        ivec3(-1, 0, 0), ivec3(1, 0, 0),
        ivec3(0, -1, 0), ivec3(0, 1, 0),
        ivec3(0, 0, -1), ivec3(0, 0, 1)
    );

    for (int i = 0; i < 6; i++) {
        ivec3 n = cell + off[i];
        if (any(lessThan(n, ivec3(0))) || any(greaterThanEqual(n, ivec3(gridSize)))) {
            continue;
        }
        accum += sampleCell(n, gridSize) * f;
    }

    imageStore(lpv_dst, atlasUV(cell, gridSize), vec4(accum, 1.0));
}
